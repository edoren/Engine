apply plugin: "com.android.application"

android {
    compileSdkVersion 24

    defaultConfig {
        applicationId "@APP_PACKAGE@"
        minSdkVersion 24
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "armeabi-v7a"
        }
        externalNativeBuild {
            cmake {
                // targets "@APP_NAME@",
                arguments = [
                    @CMAKE_ARGUMENTS@,
                    "--no-warn-unused-cli"
                ]
            }
        }
    }

    buildTypes {
        debug {
            debuggable = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    externalNativeBuild {
        cmake {
            path "@APP_ROOT_DIRECTORY@/CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            manifest.srcFile "@APP_BUILD_DIRECTORY@/AndroidManifest.xml"
            java.srcDirs = ["@APP_BUILD_DIRECTORY@/src"]
            res.srcDirs = ["@APP_BUILD_DIRECTORY@/res"]
            assets.srcDirs = ["@APP_BUILD_DIRECTORY@/assets"]
            jniLibs.srcDirs = ["@APP_BUILD_DIRECTORY@/jniLibs"]
        }
    }
}

task runProcessAssets(type: Exec) {
    commandLine "@PYTHON_EXECUTABLE@", "@APP_ROOT_DIRECTORY@/scripts/process_data.py", "@APP_ROOT_DIRECTORY@/data", "@APP_BUILD_DIRECTORY@/assets"
}

task copyRelease(type: Copy) {
    from("build/outputs/apk/release")
    into("../bin")
    include("app-release-unsigned.apk")
    rename("app-release-unsigned.apk", "@APP_NAME@-Release.apk")
}

task copyDebug(type: Copy) {
    from("build/outputs/apk/debug")
    into("../bin")
    include("app-debug.apk")
    rename("app-debug.apk", "@APP_NAME@-Debug.apk")
}

afterEvaluate {
    if (project.hasProperty("assembleRelease")) {
        assembleRelease.finalizedBy copyRelease
        externalNativeBuildRelease.dependsOn runProcessAssets
    }
    if (project.hasProperty("assembleDebug")) {
        assembleDebug.finalizedBy copyDebug
        externalNativeBuildDebug.dependsOn runProcessAssets
    }
}

dependencies {
    implementation fileTree(dir: "@APP_BUILD_DIRECTORY@/libs", include: ["*.jar"])
    implementation fileTree(dir: "@APP_ROOT_DIRECTORY@/third_party/prebuilt/android/armeabi-v7a/Release", include: ["*.jar"])
    implementation "com.android.support.constraint:constraint-layout:1.0.2"
}
